subprojects {
    apply plugin: 'java'
	apply plugin: 'application'
	apply plugin: 'ivy-publish'

    java {
	    sourceCompatibility = JavaVersion.toVersion(17)
	    targetCompatibility = JavaVersion.toVersion(17)
    }

    version = '1.0'

    repositories {
        mavenLocal()
	    mavenCentral()
        maven {
            url 'https://github.com/sjzeil/mvnrepo/raw/main'
        }
    }


    dependencies {
        implementation 'edu.odu.cs.algae:algae-client-server:4.6'
    }

    def coursePath = '/home/zeil/courses/cs361/latest/Directory/animations/'
    task deployToCourse (type: Copy, dependsOn: ['jar']) {
        from 'build/libs/'
        into coursePath
        rename { String fileName -> fileName.replace("-$project.version","")}
    }

    // Publishing

    // Credentials are loaded from ~/.gradle/gradle.properties
    if(project.hasProperty("ivyRepoUser")){
        ext.ivyRepoUser = "$ivyRepoUser";
    } else {
        ext.ivyRepoUser = "user";
    }
    if(project.hasProperty("ivyRepoPass")){
        ext.ivyRepoPass = "$ivyRepoPass";
    } else {
        ext.ivyRepoPass = "password";
    }

    publishing {
        publications {
            ivyJava(IvyPublication) {
                organisation 'edu.odu.cs.cs361'
                module 'cs361-' + project.name
                revision project.version
                descriptor.status = 'integration'   // milestone, release
                descriptor.branch = 'v4'

                from components.java
            }
        }

        repositories {
          ivy {
            name 'ivyRepo'
            url 'sftp://linux.cs.odu.edu:22/home/zeil/secure_html/ivyrepo'
            // Readable via https://www.cs.odu.edu/~zeil/ivyrepo
            credentials {
                username project.ivyRepoUser
                password project.ivyRepoPass
            }
          }
        }
    }
}
